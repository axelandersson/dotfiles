#!/usr/bin/perl -w

use strict;
use CLI;
use Text::Autoformat;

my $options = CLI::options("dryrun|n");
my $command = shift @ARGV;

CLI::usage("[--dryrun|-n] <command> <file> [<file> ...]", "Commands: 's/foo/bar/g' 'uppercase' 'lowercase' 'titlecase'") if $options->{"help"} || !$command || @ARGV == 0;

my $regexp;
my $autoformatcase;

if($command =~ /^s\/.*?\/.*?\/\w*$/) {
    $regexp = $command;
}
elsif($command eq "uppercase" || $command eq "upper" || $command eq "uc") {
    $autoformatcase = "upper";
}
elsif($command eq "lowercase" || $command eq "lower" || $command eq "lc") {
    $autoformatcase = "lower";
}
elsif($command eq "titlecase" || $command eq "title" || $command eq "tc") {
    $autoformatcase = "highlight";
}

CLI::assert_true($regexp || $autoformatcase, "Command \"$command\" not recognized");

foreach my $path (@ARGV) {
    my $olddirectory = CLI::pathbydeletinglastpathcomponent($path);
    my $oldname = CLI::lastpathcomponent($path);
    my $newname;

    if($regexp) {
        $newname = $oldname;
        eval("\$newname =~ $regexp;");
    }
    elsif($autoformatcase) {
        $newname = autoformat($oldname, {case => $autoformatcase});
        $newname =~ s/\s+$//s;
        $newname =~ s/(\.[\w\d]+)$/lc($1)/ei;
    }

    my $newpath = ($olddirectory eq ".") ? $newname : "$olddirectory/$newname";

    if($path eq $newpath) {
        print "\"$path\" -> \"$newpath\": No change\n";
        next;
    }

    if($options->{"dryrun"}) {
        print "\"$path\" -> \"$newpath\": OK (Dry run)\n";
        next;
    }

    if(rename($path, $newpath)) {
        print "\"$path\" -> \"$newpath\": OK\n";
        next;
    }

    print "\"$path\" -> \"$newpath\": $!\n";
}
