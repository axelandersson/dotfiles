#!/usr/bin/env perl

use strict;
use warnings;
use CLI;
use CLI::File;
use String::Diff;
use Text::Autoformat;

my $options = CLI::options("dryrun|n", "interactive|i", "color|c");
my $command = shift @ARGV;
my @files = @ARGV;

CLI::usage("[--dryrun|-n] [--interactive|-i] <command> <file> [<file> ...]", "Commands: 's/foo/bar/g' 'uppercase' 'lowercase' 'titlecase'") if $options->{"help"} || !$command || @files == 0;

my $regexp;
my $autoformatcase;

if($command =~ /^s\/.*?\/.*?\/\w*$/) {
    $regexp = $command;
} elsif($command eq "uppercase" || $command eq "upper" || $command eq "uc") {
    $autoformatcase = "upper";
} elsif($command eq "lowercase" || $command eq "lower" || $command eq "lc") {
    $autoformatcase = "lower";
} elsif($command eq "titlecase" || $command eq "title" || $command eq "tc") {
    $autoformatcase = "highlight";
}

CLI::asserttrue($regexp || $autoformatcase, "Command \"$command\" not recognized");

foreach my $oldpath (@files) {
    my $newpath = renamedpath($oldpath, $regexp, $autoformatcase);
    my $description;
    my $move = 0;

    if($options->{"color"}) {
        my($oldstartcode, $oldstopcode) = CLI::coloredstringcodes("white,none,red");
        my($newstartcode, $newstopcode) = CLI::coloredstringcodes("white,none,green");

        my($olddiff, $newdiff) = String::Diff::diff($oldpath, $newpath,
            "remove_open" => $oldstartcode,
            "remove_close" => $oldstopcode,
            "append_open" => $newstartcode,
            "append_close" => $newstopcode
        );

        $description = "$olddiff -> $newdiff";
    } else {
        $description = "$oldpath -> $newpath";
    }

    if($oldpath eq $newpath) {
        println "$description: No change";
    } elsif($options->{"dryrun"}) {
        println "$description: OK (Dry run)";
    } elsif($options->{"interactive"}) {
        CLI::asserttrue(CLI::isterminalinteractive(), "Terminal is not interactive");

        my $answer = CLI::ask($description . ": OK? [y]");

        if($answer =~ /^(|y|yes)$/i) {
            $move = 1;
        }
    } else {
        $move = 1;
    }

    if($move) {
        if(CLI::File::move($oldpath, $newpath)) {
            println "$description: OK";
        }
    }
}



sub renamedpath {
    my $path = shift;
    my $regexp = shift;
    my $autoformatcase = shift;

    my $olddirectory = CLI::pathbydeletinglastpathcomponent($path);
    my $oldname = CLI::lastpathcomponent($path);
    my $newname;

    if($regexp) {
        $newname = $oldname;
        eval("\$newname =~ $regexp;");
    } elsif($autoformatcase) {
        my $autoformatoptions = {
            "case" => $autoformatcase,
        };

        my $oldextension;

        if($oldname =~ /^(.+?)(\.[\w\d]+)$/i) {
            $oldname = $1;
            $oldextension = $2;
        }

        if($oldname =~ /^(.+?) - (.+?)$/) {
            my $newfirstpart = autoformat($1, $autoformatoptions);
            my $newsecondpart = autoformat($2, $autoformatoptions);

            $newfirstpart =~ s/\s+$//s;
            $newsecondpart =~ s/\s+$//s;

            $newname = "$newfirstpart - $newsecondpart";
        } else {
            $newname = autoformat($oldname, $autoformatoptions);
        }

        $newname =~ s/\s+$//s;
        $newname =~ s/\b([A-Z])(\1+)\b/uc($1 . $2)/ei;
        $newname .= $oldextension;
    }

    if($olddirectory eq ".") {
        return $newname;
    } else {
        return CLI::pathbyappendingpathcomponents($olddirectory, $newname);
    }
}
